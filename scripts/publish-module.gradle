apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.artifactory'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream()) // load local.properties
def CI_USER = properties.getProperty("CI_USER") // read property from local.properties
def CI_PASSWORD = properties.getProperty("CI_PASSWORD")


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                tasks.named("generateMetadataFileForReleasePublication").configure { dependsOn("androidSourcesJar") }
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Stream Chat official Android SDK'
                    licenses {
                        license {
                            name = 'Stream License'
                            url = 'https://github.com/GetStream/stream-chat-android/blob/main/LICENSE'
                        }
                    }
                    url = 'https://github.com/AlanChienHouzz/stream-chat-android'
                    developers {
                        developer {
                            id = 'tschellenbach'
                            name = 'Thierry Schellenbach'
                            email = 'thierry@getstream.io'
                        }
                        developer {
                            id = 'jcminarro'
                            name = 'Jc Miñarro'
                            email = 'josecarlos@getstream.io'
                        }
                        developer {
                            id = 'samiuelson'
                            name = 'Samuel Urbanowicz'
                            email = 'samuel@getstream.io'
                        }
                        developer {
                            id = 'adasiewiczr'
                            name = 'Rafal Adasiewicz'
                            email = 'rafal@getstream.io'
                        }
                        developer {
                            id = 'ogkuzmin'
                            name = 'Oleg Kuzmin'
                            email = 'oleg@getstream.io'
                        }
                        developer {
                            id = 'zsmb13'
                            name = 'Márton Braun'
                            email = 'marton@getstream.io'
                        }
                        developer {
                            id = 'leandroBorgesFerreira'
                            name = 'Leandro Borges Ferreira'
                            email = 'leandro@getstream.io'
                        }
                        developer {
                            id = 'bychkovdmitry'
                            name = 'Dmitrii Bychkov'
                            email = 'dmitrii@getstream.io'
                        }
                        developer {
                            id = 'alan'
                            name = 'Alan Chien'
                            email = 'alan.chien@houzz.com'
                        }
                    }
                }
            }
        }
    }
    artifactory {
        contextUrl = 'http://artifactory.ivyco.net/artifactory/mobile-android/'
        publish {
            repository {
                repoKey = "libs-release-local"
                username = CI_USER
                password = CI_PASSWORD
            }
            defaults {
                publishArtifacts = true
                publishPom = true
                publishIvy = false
            }
        }
    }
    artifactoryPublish {
        publications(publishing.publications.release)
    }
}
